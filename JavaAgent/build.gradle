plugins {
    id("java")
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

configurations {
    exportLib
    compileOnly.extendsFrom(exportLib)
}

repositories {
    maven {
        name = "Spongepowered"
        url = uri("https://repo.spongepowered.org/maven")
    }
}

dependencies {
    compileOnly(project(":Core"))

    exportLib("org.ow2.asm:asm:9.4")
    exportLib("org.ow2.asm:asm-commons:9.4")
    exportLib("org.ow2.asm:asm-tree:9.4")
    exportLib("org.ow2.asm:asm-util:9.4")
    exportLib("com.google.guava:guava:31.1-jre")
    exportLib('com.google.code.gson:gson:2.8.9')
    exportLib('commons-io:commons-io:2.11.0')
    exportLib('net.fabricmc:sponge-mixin:0.12.4+mixin.0.8.5') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor')
}

jar {
    manifest {
        attributes(
            "Premain-Class": "dev.lynith.javaagent.AgentMain",
            "Can-Redefine-Classes": "true",
            "Can-Retransform-Classes": "true"
        )
    }

    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)

    from {
        configurations.exportLib.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}